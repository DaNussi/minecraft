/* groovylint-disable CompileStatic, NestedBlockDepth, VariableTypeRequired */

LinkedList<String> changedEggFiles = [] as LinkedList<String>
def eggConfigs = [] as LinkedList<Map>
def eggTestingStages = [:]

// store the resolved pelican user object (id/username/email) for downstream stages
def PELICAN_USER = [:]

pipeline {
    agent any

    environment {
        PELICAN_BASE_URL = credentials('jenkins-pelican-base-url')
        PELICAN_USER_EMAIL    = credentials('jenkins-pelican-user')
        PELICAN_TOKEN = credentials('jenkins-pelican-token')
    }

    stages {
        stage('Validate environment variables') {
            steps {
                script {
                    if (PELICAN_BASE_URL?.trim()?.length() == 0) {
                        error 'PELICAN_BASE_URL parameter is required'
                    }
                    if (PELICAN_USER_EMAIL?.trim()?.length() == 0) {
                        error 'PELICAN_USER_EMAIL parameter is required'
                    }
                    if (PELICAN_TOKEN?.trim()?.length() == 0) {
                        error 'PELICAN_TOKEN parameter is required'

                    }
                }
            }
        }

        stage('Resolve pelican user id') {
            steps {
                script {
                    def response = sh(
                        script: 'curl -s -o .jenkins/user-request.json -w "%{http_code}\n" -X GET -H "Accept: application/json" -H "Authorization: Bearer $PELICAN_TOKEN" -- $PELICAN_BASE_URL/users',
                        returnStdout: true
                    ).trim()

                    if(response.startsWith('2')) {
                        echo 'Successfully retrieved pelican users'
                        def usersJson = readJSON file: '.jenkins/user-request.json'
                        def pelicanUsers = usersJson['data'].collect { userJson ->
                            [
                                id: userJson['attributes']['id'],
                                username: userJson['attributes']['username'],
                                email: userJson['attributes']['email']
                            ]
                        }

                        echo "Found ${pelicanUsers.size()} users"

                        // Match by email (case-insensitive), fall back to username if not found.
                        def target = PELICAN_USER_EMAIL?.trim()?.toLowerCase()
                        def matchedUsers = pelicanUsers.findAll { u ->
                            (u.email ?: '').toLowerCase() == target
                        }

                        if (matchedUsers.size() == 1) {
                            def pelicanUser = matchedUsers[0]
                            PELICAN_USER = pelicanUser
                            echo "Resolved Pelican user id: ${pelicanUser.id} for user: ${pelicanUser.username} (${pelicanUser.email})"
                        } else if (matchedUsers.size() > 1) {
                            error "Multiple Pelican users matched. Please ensure the PELICAN_USER_EMAIL is correct and unique."
                        } else {
                            error "No Pelican user matched. Please ensure PELICAN_USER_EMAIL exists in the pelican panel."
                        }


                    } else {
                        error "Failed to retrieve pelican users, response code: ${response}"
                    }
                }
            }
        }

        stage('Detect egg changes') {
            steps {
                script {
                    def targetBranch = env.CHANGE_TARGET ?: 'main'
                    echo "Comparing changes against branch: origin/${targetBranch}"

                    sh 'git show-ref'

                    def eggsConfig = readJSON file: '.jenkins/eggs.json'

                    changedEggFiles = []
                    eggConfigs += eggsConfig['eggs']

                    eggConfigs.each { eggConfig ->
                        echo "Found egg configuration: ${eggConfig}"

                        String currentChangedEggDiffs = sh(
                            script: "git diff --name-only origin/${targetBranch} -- ${eggConfig['path']}",
                            returnStdout: true
                        )
                        .trim()

                        if (!currentChangedEggDiffs.isEmpty()) {
                            def currentChangedEggFiles = currentChangedEggDiffs
                                .split('\n')
                                .findAll { file -> file.endsWith('.json') }
                                //.findAll { file -> file.endsWith('.json') && fileExists(file) }

                            echo "Detected ${currentChangedEggFiles.size()} changed egg files in path: ${eggConfig['path']}"

                            changedEggFiles += currentChangedEggFiles
                        } else {
                            echo "No changes detected in egg path: ${eggConfig['path']}"
                        }
                    }

                    echo 'Changed egg file:'
                    changedEggFiles.each { changedEggFile ->
                        echo changedEggFile
                    }
                }
            }
        }

        stage('Testing egg\'s'){
            steps {
                script {
                    for (int pi = 0; pi < changedEggFiles.size(); pi++) {
                        String eggFile = changedEggFiles[pi]
                        eggTestingStages["Testing Egg ${eggFile}"] = testEgg(eggFile)
                    }
                    parallel eggTestingStages
                }
            }
        }
    }
}


def testEgg(String eggFile) {
    return {
        stage("Testing Egg ${eggFile}") {

            stage("Updating Egg UUID ${eggFile}") {
                script {
                    def eggJson = readJSON file: eggFile

                    def gitUUID = env.GIT_COMMIT as String
                    def fileUUID = eggFile
                    def eggUUID = UUID.nameUUIDFromBytes((fileUUID + '@' + gitUUID).getBytes()).toString()

                    eggJson['uuid'] = eggUUID

                    writeJSON file: eggFile, json: eggJson

                    echo "Set Egg ${eggFile} uuid to ${eggUUID}"
                }
            }

            stage("Import Egg ${eggFile}") {
                script {
                    echo "Importing egg file: ${eggFile}"

                    def response_file = eggFile.replace('.json', '.import.json')

                    def response = sh(
                            script: """
                                curl -s -X POST --data-binary "@${eggFile}" -o "${response_file}" -w '%{http_code}\\n' -H 'Accept: application/json' -H "Authorization: Bearer ${env.PELICAN_TOKEN}" -- "${env.PELICAN_BASE_URL}/eggs/import"
                            """,
                            returnStdout: true
                    ).trim()

                    if(response.startsWith('2')) {
                        echo "Successfully imported egg ${eggFile}"

                    } else {
                        error "Failed to import egg ${eggFile}, response code: ${response}"
                    }
                }
            }

            stage("Delete Egg ${eggFile}") {
                script {
                    echo "Deleting egg: ${eggFile}"

                    def response_file = eggFile.replace('.json', '.delete.json')

                    def gitUUID = env.GIT_COMMIT as String
                    def fileUUID = eggFile
                    def eggUUID = UUID.nameUUIDFromBytes((fileUUID + '@' + gitUUID).getBytes()).toString()

                    def response = sh(
                            script: """
                                curl -s -X DELETE -o "${response_file}" -w '%{http_code}\\n' -H 'Accept: application/json' -H "Authorization: Bearer ${env.PELICAN_TOKEN}" -- "${env.PELICAN_BASE_URL}/eggs/${eggUUID}/uuid"
                            """,
                            returnStdout: true
                    ).trim()

                    if(response.startsWith('2')) {
                        echo "Successfully deleted egg ${eggFile}"

                    } else {
                        error "Failed to delete egg ${eggFile} with uuid ${eggUUID}, response code: ${response}"
                    }
                }
            }
        }
    }
}
